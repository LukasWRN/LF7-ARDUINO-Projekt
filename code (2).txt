#include <SPI.h>
#include <MFRC522.h>

#define SS_PIN 10
#define RST_PIN 9
MFRC522 mfrc522(SS_PIN, RST_PIN);   // Create MFRC522 instance.

int LEDblau = 3; // Farbe blau an Pin 3
int LEDrot = 6; // Farbe rot an Pin 6
int LEDgruen = 5; // Farbe gruen an Pin 5
int speaker = 8; // Speaker an Pin 8
int trigPin = 4; // Trig Pin des Ultraschallsensors
int echoPin = 2; // Echo Pin des Ultraschallsensors
int buttonPin = 7; // Button an Pin 7
int p = 1000; // p ist eine Pause mit 1000ms also 1 Sekunde
int brightness = 255; // Zahlenwert zwischen 0 und 255 – gibt die Leuchtstärke der einzelnen Farbe an
int dunkel = 0; // Zahlenwert 0 bedeutet Spannung 0V – also LED aus

bool objectDetected = false;
unsigned long lastSoundTime = 0;
bool ultrasonicEnabled = false;
bool rfidEnabled = false;
int buttonState = 0;
int lastButtonState = 0;
int buttonPressCount = 0;

void setup() 
{
  Serial.begin(9600);   // Initiate a serial communication
  SPI.begin();      // Initiate SPI bus
  mfrc522.PCD_Init();   // Initiate MFRC522
  Serial.println("Bitte Karte an den Leser halten...");
  Serial.println();

  pinMode(LEDblau, OUTPUT);
  pinMode(LEDgruen, OUTPUT);
  pinMode(LEDrot, OUTPUT);
  pinMode(trigPin, OUTPUT);
  pinMode(echoPin, INPUT);
  pinMode(buttonPin, INPUT_PULLUP);

  analogWrite(LEDblau, dunkel); 
  analogWrite(LEDgruen, dunkel);
  analogWrite(LEDrot, dunkel); 
}

void loop() 
{
  buttonState = digitalRead(buttonPin);
  
  if (buttonState != lastButtonState) 
  {
    if (buttonState == LOW) 
    {
      buttonPressCount++;
      if (buttonPressCount == 1) 
      {
        ultrasonicEnabled = true;
        Serial.println("Ultraschallsensor aktiviert");
      } 
      else if (buttonPressCount == 2) 
      {
        rfidEnabled = true;
        Serial.println("RFID Leser aktiviert");
      } 
      else if (buttonPressCount == 3) 
      {
        ultrasonicEnabled = false;
        rfidEnabled = false;
        buttonPressCount = 0;
        Serial.println("Alle Sensoren deaktiviert");
      }
    }
    delay(50); // debounce delay
  }
  lastButtonState = buttonState;

  if (ultrasonicEnabled && isObjectInProximity()) 
  {
    if (!objectDetected) 
    {
      objectDetected = true;
      unsigned long currentMillis = millis();
      if (currentMillis - lastSoundTime > 2000) 
      {
        playOpenSound(); // Öffnungssound abspielen
        lastSoundTime = currentMillis;
        Serial.println("Karte jetzt dran halten...");
      }
    }
    
    // Blaue LED einschalten
    analogWrite(LEDrot, dunkel);
    analogWrite(LEDgruen, dunkel);
    analogWrite(LEDblau, brightness); // blau einschalten

    if (rfidEnabled) 
    {
      // Look for new cards
      if (!mfrc522.PICC_IsNewCardPresent()) 
      {
        return;
      }
      // Select one of the cards
      if (!mfrc522.PICC_ReadCardSerial()) 
      {
        return;
      }
      // Show UID on serial monitor
      Serial.print("Karte (UID):");
      String content = "";
      byte letter;
      for (byte i = 0; i < mfrc522.uid.size; i++) 
      {
        Serial.print(mfrc522.uid.uidByte[i] < 0x10 ? " 0" : " ");
        Serial.print(mfrc522.uid.uidByte[i], HEX);
        content.concat(String(mfrc522.uid.uidByte[i] < 0x10 ? " 0" : " "));
        content.concat(String(mfrc522.uid.uidByte[i], HEX));
      }
      Serial.println();
      Serial.print("Nachricht : ");
      content.toUpperCase();
      if (content.substring(1) == "FE D3 31 17") // change here the UID of the card/cards that you want to give access
      {
        Serial.println("Zugriff gewährt!");
        Serial.println();
        
        analogWrite(LEDgruen, brightness); // gruen einschalten
        analogWrite(LEDblau, dunkel);
        analogWrite(LEDrot, dunkel);
        playStarWars(); // Star Wars Sound abspielen
        delay(p); // pause
        analogWrite(LEDgruen, dunkel); // gruen ausschalten
      }
      else 
      {
        Serial.println("Zugriff verweigert!");

        analogWrite(LEDrot, brightness); // rot einschalten
        analogWrite(LEDgruen, dunkel);
        analogWrite(LEDblau, dunkel);
        playErrorTone(); // Error-Ton abspielen
        delay(p); // pause
        analogWrite(LEDrot, dunkel); // rot ausschalten
      }
    }
  } 
  else 
  {
    objectDetected = false;
    analogWrite(LEDrot, dunkel); 
    analogWrite(LEDgruen, dunkel);
    analogWrite(LEDblau, dunkel);
  }
}

bool isObjectInProximity() 
{
  digitalWrite(trigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(trigPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trigPin, LOW);

  long duration = pulseIn(echoPin, HIGH);
  float distance = (duration * 0.0343) / 2;
  
  if (distance <= 5.0) 
  {
    return true;
  } 
  else 
  {
    return false;
  }
}

void playErrorTone() 
{
  tone(speaker, 100, 500); // 100Hz Ton für 500ms abspielen
  delay(500);
  tone(speaker, 200, 500); // 200Hz Ton für 500ms abspielen
  delay(500);
  noTone(speaker);
}

void playOpenSound() 
{
  // Eine einfache angenehme Melodie
  int melody[] = { 262, 294, 330 };
  int noteDurations[] = { 150, 150, 150 };

  for (int thisNote = 0; thisNote < 3; thisNote++) 
  {
    tone(speaker, melody[thisNote], noteDurations[thisNote] * 0.8);
    delay(noteDurations[thisNote] * 1.30);
    noTone(speaker);
  }
}

void playStarWars() 
{
  // Star Wars theme melody (simplified)
  int melody[] = {
    440, 440, 440, 349, 523, 440, 349, 523, 440
  };
  int noteDurations[] = {
    500, 500, 500, 350, 150, 500, 350, 150, 650
  };
  for (int thisNote = 0; thisNote < 9; thisNote++) 
  {
    int noteDuration = noteDurations[thisNote];
    if (melody[thisNote] != 0) {
      tone(speaker, melody[thisNote], noteDuration);
    }
    int pauseBetweenNotes = noteDuration * 1.30;
    delay(pauseBetweenNotes);
    noTone(speaker);
  }
}
